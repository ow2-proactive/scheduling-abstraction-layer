
apply plugin: 'spring-boot'
apply plugin: 'com.github.kt3k.coveralls'
apply from: "$rootDir/gradle/ext/coding-format.gradle"
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.jk1.dependency-license-report'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repository.activeeon.com/content/groups/proactive/' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.8.RELEASE'
        classpath "com.diffplug.gradle.spotless:spotless:2.4.0"
        classpath "org.ow2.proactive:coding-rules:2.0.0"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.7.1'
        classpath "io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1"
        classpath 'com.github.jk1:gradle-license-report:1.7'
    }

    dependencies {
        delete "gradle/ext"
        ant.unjar src: configurations.classpath.find { it.name.startsWith("coding-rules") }, dest: 'gradle/ext'
    }
}

bootRepackage {
    enabled = false
}

def mainClassName = 'org.ow2.proactive.sal.service.Application'

springBoot {
    mainClass = mainClassName
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'project-report'
    apply plugin: 'spring-boot'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'

    group = 'org.ow2.proactive'
    version = version

    defaultTasks 'clean', 'build', 'check'

    repositories {
        if (project.hasProperty('local')) {
            mavenLocal()
        }
        mavenCentral()
        maven { url 'http://repository.activeeon.com/content/groups/proactive/' }
    }

    if (project.hasProperty('ci')) {
        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        }
    }

    configurations.all {
        resolutionStrategy.force "org.ow2.proactive:sal:${version}"
    }

    rootProject.buildscript.repositories.each {
        repositories.add(it)
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    dependencyManagement {
        imports {
            mavenBom "org.ow2.proactive:parent-bom:${version}"
        }
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }

    dependencies {
        testCompile 'com.google.truth:truth'
        testCompile 'junit:junit'
        testCompile 'org.mockito:mockito-core'
    }

    ext {
        generatedSrcDir = "$project.buildDir/src/generated/java"
    }

    sourceSets {
        integrationTest {
            java.srcDir file('src/integration-test/java')
            resources.srcDir file('src/integration-test/resources')

            compileClasspath = sourceSets.main.output + configurations.integrationTestCompile
            runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntime
        }
    }

    configurations {
        // The following module is excluded to avoid clashes when embedded inside the ProActive Scheduler
        all*.exclude module: 'spring-boot-starter-logging'

        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                snapshotRepository(url: "http://repository.activeeon.com/content/repositories/snapshots/") {
                    authentication(userName: "${System.getProperty('nexusUsername')}",
                            password: "${System.getProperty('nexusPassword')}")
                }
                repository(url: "http://repository.activeeon.com/content/repositories/releases/") {
                    authentication(userName: "${System.getProperty('nexusUsername')}",
                            password: "${System.getProperty('nexusPassword')}")
                }
            }
        }
    }

    sonarqube {
        properties {
            property "sonar.projectKey", "$project.group:scheduling-abstraction-layer"
            property "sonar.projectName", "scheduling-abstraction-layer"
            property "sonar.jdbc.driverClassName", "org.postgresql.Driver"
            property "sonar.language", "java"
            property "sonar.sources", "src/main"
            property 'sonar.jacoco.reportPath', "${buildDir}/jacoco/testDebug.exec"
            property 'sonar.junit.reportsPath', "${buildDir}/test-results"
        }
    }

    idea {
        module {
            testSourceDirs += file('src/integration-test/java')
            testSourceDirs += file('src/integration-test/resources')

            scopes.TEST.plus += [
                    configurations.integrationTestCompile,
                    configurations.integrationTestRuntime
            ]
        }
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
        classpath += sourceSets.test.runtimeClasspath

        jacoco {
            destinationFile = file("$buildDir/jacoco/jacocoIntegrationTest.exec")
        }

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    jacocoTestReport {
        executionData test, integrationTest

        sourceSets project.sourceSets.main

        reports {
            html.enabled = true
            xml.enabled = true
        }
    }

    coveralls {
        sourceDirs = allprojects.sourceSets.main.allSource.srcDirs.flatten()
        jacocoReportPath = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }

    test {
        jacoco {
            destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        }
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '3.3'
    }

    check.dependsOn integrationTest

    jacocoTestReport.dependsOn check
    tasks.coveralls.dependsOn jacocoTestReport

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
    }
}
